apply plugin: 'jacoco'

jacoco {
    toolVersion = libs.versions.jacoco.get()
}

def filesToExcludeFromJacocoReport = project.findProperty('jacocoExclusions') ?: []

jacocoTestReport {
    dependsOn test

    reports {
        xml.required.set(false)
        csv.required.set(false)
        html.required.set(true)
    }

    afterEvaluate {
        classDirectories.setFrom(
                files(classDirectories.files.collect {
                    fileTree(dir: it, exclude: filesToExcludeFromJacocoReport)
                })
        )
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.80
            }
        }
    }

    afterEvaluate {
        classDirectories.setFrom(
                files(classDirectories.files.collect {
                    fileTree(dir: it, exclude: filesToExcludeFromJacocoReport)
                })
        )
    }
}

test {
    finalizedBy jacocoTestReport
}

check {
    dependsOn jacocoTestReport, jacocoTestCoverageVerification
}
