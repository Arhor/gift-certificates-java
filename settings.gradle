pluginManagement {
    plugins {
        id 'org.flywaydb.flyway' version settings['version.flyway']
    }
}

rootProject.name = 'gift-certificates-java'

include ':app-server'
include ':app-server:module-repository'
include ':app-server:module-service'
include ':app-server:module-web'

enableFeaturePreview 'VERSION_CATALOGS'

// dependency version should be declared in 'gradle.properties' with 'version.' prefix
def dependencyVersions = properties
        .findAll { it.key.startsWith 'version.' }
        .collectEntries { [it.key - 'version.', it.value] }

dependencyResolutionManagement {
    versionCatalogs {
        libs {
            dependencyVersions.each {
                version it.key, it.value
            }
        }
    }
}
